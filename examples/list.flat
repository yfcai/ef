. PRELUDE

const : ∀ β γ. β -> γ -> β
const = Λ. λx : _0_. λy : _1_. x

flip : ∀α β γ. (α → β → γ) → (β → α → γ)
flip = Λ. λf : _0_. λy : _1_. λx : _2_. f x y

id : ∀ι. ι → ι
id = Λ. λx : _0_. x

compose : ∀α β γ. (β → γ) → (α → β) → (α → γ)
compose = Λ. λg : _0_. λf : _1_. λx : _2_. g (f x)

. CONJUNCTION

type Pair α β = ∀γ. (α → β → γ) → γ

pair : ∀α β. α → β → Pair α β
pair = Λ. λx : _0_. λy : _1_. Λ. λf : _2_. f x y

proj₁ : ∀α β. Pair α β → α
proj₁ = Λ. λp : _0_. p const

proj₂ : ∀α β. Pair α β → β
proj₂ = Λ. λp : _0_. p (flip const)


. DISJUNCTION

type Either α β = ∀γ. (α → γ) → (β → γ) → γ

inj₁ : ∀α β. α → Either α β
inj₁ = Λ. λx : _0_. Λ. λf : _1_. λg : _2_. f x

inj₂ : ∀α β. β → Either α β
inj₂ = Λ. λy : _0_. Λ. λf : _1_. λg : _2_. g y

. a disjunction is its own elimination form


. LIST

type List α = ∀β. β → (α → β → β) → β

. List is desugared to (∀α. List α)
nil : ∀α. List α
nil = const

cons : ∀α. α → List α → List α
cons = Λ. λx : _0_. λxs : _1_. Λ. λz : _2_. λf : _3_. (f x (xs z f))

. BӦHM-BERADUCCI ENCODING FOR PATTERN MATCHING ON LISTS

type MatchList η = ∀ξ. ξ → (η → List η → ξ) → ξ

matchList : ∀α. List α → MatchList α
matchList = Λ. λxs : _0_.
  xs const
     (λx : _1_. λys : MatchList α. Λ. λz : _3_. λf : _4_. f x (ys nil cons))

. Even without annotations,
. the following bodies are ill-typed:
.   xs nil cons
.   xs nil f
map : ∀α β. (α → β) → List α → List β
map = Λ. λf : _0_. λxs : _1_. xs nil (compose cons f)

concat : ∀α. List α → List α → List α
concat = Λ. λxs : _0_. λys : _1_. xs ys cons

concatMap : ∀α β. (α → List β) → List α → List β
concatMap = Λ. λf : _0_. λxs : _1_. xs nil (compose concat f)

filter : ∀α. (α → Bool) → List α → List α
filter = Λ. λp : _0_. λxs : _1_. xs nil (λx : _3_. if p x then cons x else id)

. USAGE

head : ∀α. List α → α
head = Λ. λxs : _0_. xs ??? const

tail : ∀α. List α → List α
tail = Λ. λxs : _0_. matchList xs ??? (flip const)

ana-ℤ : ℤ → List ℤ
ana-ℤ = λn : ℤ. iterate n nil cons

sum : List ℤ → ℤ
sum = λns : List ℤ. ns 0 +

list1-5 : List ℤ
list1-5 = ana-ℤ 5

head nil

tail nil

sum (cons 1 (cons 2 (cons 3 (cons 4 (cons 5 nil)))))

sum (map (* 2) list1-5)
