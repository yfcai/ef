. PRELUDE

. ill-typed with the signature
.   auto : ∀α. α → α
. even as we do not annotate the argument type.
auto : (∀ω. ω → ω) → (∀ω. ω → ω)
auto = λx : _0_. x x

. Ω is always ill-typed.
.Ω : ∃i. i
.Ω = (λx : _0_. x x) (λx : _1_. x x)

const : ∀ β γ. β -> γ -> β
const = λx : _0_. λy : _1_. x

flip : ∀α β γ. (α → β → γ) → (β → α → γ)
flip = λf : _0_. λy : _1_. λx : _2_. f x y

id : ∀ι. ι → ι
id = λx : _0_. x

compose : ∀β γ. (β → γ) → ∀α. (α → β) → (α → γ)
compose = λg : _0_. λf : _1_. λx : _2_. g (f x)


. CONJUNCTION

type Pair α β = ∀γ. (α → β → γ) → γ

pair : ∀α β. α → β → Pair α β
pair = λx : _0_. λy : _1_. λf : _2_. f x y

proj₁ : ∀α β. Pair α β → α
proj₁ = λp : _0_. p const

proj₂ : ∀α β. Pair α β → β
proj₂ = λp : _0_. p (flip const)


. DISJUNCTION

type Either α β = ∀γ. (α → γ) → (β → γ) → γ

inj₁ : ∀α β. α → Either α β
inj₁ = λx : _0_. λf : _1_. λg : _2_. f x

inj₂ : ∀α β. β → Either α β
inj₂ = λy : _0_. λf : _1_. λg : _2_. g y

. a disjunction is its own elimination form


. LIST

type List α = ∀β. β → (α → β → β) → β

. List is desugared to (∀α. List α)
nil : ∀α. List α
nil = const

cons : ∀α. α → List α → List α
cons = λx : _0_. λxs : _1_. λz : _2_. λf : _3_. (f x (xs z f))

. BӦHM-BERADUCCI ENCODING FOR PATTERN MATCHING ON LISTS

type MatchList η = ∀ξ. ξ → (η → List η → ξ) → ξ

matchList : ∀α. List α → MatchList α
matchList = λxs : _0_.
  xs const
     (λx : _1_. λys : MatchList α. λz : _3_. λf : _4_. f x (ys nil cons))

. Even without annotations,
. the following bodies are ill-typed:
.   xs nil cons
.   xs nil f
map : ∀α β. (α → β) → List α → List β
map = λf : _0_. λxs : _1_. xs nil (compose cons f)

concat : ∀α. List α → List α → List α
concat = λxs : _0_. λys : _1_. xs ys cons

concatMap : ∀α β. (α → List β) → List α → List β
concatMap = λf : _0_. λxs : _1_. xs nil (compose concat f)

filter : ∀α. (α → Bool) → List α → List α
filter = λp : _0_. λxs : _1_. xs nil (λx : _3_. if p x then cons x else id)

. USAGE

head : ∀α. List α → α
head = λxs : _0_. xs ??? const

tail : ∀α. List α → List α
tail = λxs : _0_. matchList xs ??? (flip const)

ana-ℤ : ℤ → List ℤ
ana-ℤ = λn : _0_. iterate n nil cons

sum : List ℤ → ℤ
sum = λns : _0_. ns 0 +

list1-5 : List ℤ
list1-5 = ana-ℤ 5

head nil

tail nil

sum (cons 1 (cons 2 (cons 3 (cons 4 (cons 5 nil)))))

sum (map (* 2) list1-5)
